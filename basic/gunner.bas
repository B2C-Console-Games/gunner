1000 PRINT "DATALINE SYSTEMS LIMITED  25-JULY-70"
1010 PRINT "B A S I C  USER LIBRARY PROGRAM--GUNNER"
1020 PRINT"THIS TIME-SHARING DEMONSTRATION SIMULATES"
1030 PRINT"THE RESULTS OF FIRING A FIELD ARTILLERY WEAPON."
1040 PRINT
1050 PRINT"YOU ARE THE OFFICER-IN-CHARGE, GIVING ORDERS TO THE GUN"
1060 PRINT"CREW, TELLING THEM THE DEGREES OF ELEVATION YOU ESTIMATE"
1070 PRINT"WILL PLACE THE PROJECTILE ON TARGET. A HIT WITHIN 100"
1080 PRINT"YARDS OF THE TARGET WILL DESTROY IT."
1090 PRINT
1100 PRINT"MAXIMUM RANGE OF YOUR GUN IS 46500 YARDS."
1110 REM T=DISTANCE TO TARGET
1120 REM I=DISTANCE OF SHOT
1130 REM E=DISTANCE OVER(NEGATIVE), OR UNDER (POSITIVE) TARGET
1140 LET T=43000-30000*RND(X)
1150 LET S=0
1160 GOTO 1410
1170 PRINT"MIN. ELEVTION OF GUN IS ONE DEGREE."
1180 GOTO 1430
1190 PRINT"MAX. ELEVATION OF GUN IS 89 DEGREES."
1200 GOTO 1430
1210 PRINT"OVER TARGET BY";ABS(E);"YARDS."
1220 GOTO 1430
1230 PRINT"SHORT OF TARGET BY";ABS(E);"YARDS."
1240 GOTO 1430
1250 GOTO 1260
1260 PRINT"***TARGET DESTROYED***  ";S;"ROUNDS EXPENDED"
1270 PRINT
1280 PRINT
1290 PRINT
1300 PRINT
1310 PRINT
1320 PRINT
1330 PRINT
1340 PRINT
1350 PRINT
1360 PRINT
1370 PRINT
1380 PRINT
1390 PRINT"THE FORWARD OBSERVER HAS SIGHTED MORE ENEMY ACTIVITY."
1400 GOTO 1140
1410 PRINT"   DISTANCE TO THE TARGET IS";INT(T);"YARDS"
1420 PRINT
1430 PRINT
1440 PRINT"ELEVATION:";
1450 INPUT B!
1460 IF B!>89 THEN 1190
1470 IF B!<1 THEN 1170
1480 LET S=S+1
1490 LET B2=2*B!/57.3
1500 LET I=46500*SIN(B2)
1510 LET X=T-I
1520 LET E=INT(X)
1530 IF ABS(E)<100 THEN 1250
1540 IF E>100 THEN 1230
1550 IF E<-100 THEN 1210
1560 END
